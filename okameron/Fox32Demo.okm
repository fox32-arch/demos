MODULE Fox32Demo;
    ASM "start.asm";
    EXTERN PROCEDURE new_window, destroy_window, draw_str_to_overlay, get_window_overlay_number, draw_widgets_to_window,
                    OKMNextWinEvent, save_state_and_yield_task, start_dragging_window, handle_widget_click: LONG;
    EXTERN eventArgs: ARRAY 8 OF LONG;
    EXTERN EVENT_TYPE_MOUSE_CLICK, EVENT_TYPE_BUTTON_CLICK, WIDGET_TYPE_BUTTON: LONG;

    TYPE Fox32OSButtonWidget = RECORD
        next, id, type, text, fgColor, bgColor, width: INT;
        x, y: SHORT;
    END;

    VAR window: ARRAY 36 OF CHAR;
        button: Fox32OSButtonWidget;

    PROCEDURE Main();
    BEGIN
        button.type := 0;
        button.next := 0;
        button.id := 0;
        button.text := "click me!";
        button.fgColor := 0FF000000H;
        button.bgColor := 0FFFFFFFFH;
        button.width := 88;
        button.x := 16;
        button.y := 112;
        new_window(PTROF(window),"Okameron Demo",256,128,128,128,0,PTROF(button));
        draw_str_to_overlay("hello okameron world :3",8,24,0FFFFFFFFH,0FF000000H,get_window_overlay_number(PTROF(window)));
        draw_widgets_to_window(PTROF(window));
        WHILE 1 DO
            OKMNextWinEvent(PTROF(window));
            IF eventArgs[0] = PTROF(EVENT_TYPE_MOUSE_CLICK) THEN
                IF (eventArgs[1] <| 8) & (eventArgs[2] <| 16) THEN
                    destroy_window(PTROF(window));
                ELSIF eventArgs[2] <| 16 THEN
                    start_dragging_window(PTROF(window));
                END;
                handle_widget_click(PTROF(window));
            ELSIF eventArgs[0] = PTROF(EVENT_TYPE_BUTTON_CLICK) THEN
                IF eventArgs[1] = 0 THEN
                    draw_str_to_overlay("button clicked!",8,40,0FFFFFFFFH,0FF000000H,get_window_overlay_number(PTROF(window)));
                END;
            END;
            save_state_and_yield_task();
        END;
    END;
END.
